#lang racket


(require racket/engine
         "mk.rkt"
         "relational_basics.rkt"
         "normalize.rkt"
         "relational_NbE.rkt"
         (except-in "basics.rkt" fresh extend-env))
(provide solve solve-arg)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Solve-arg: takes a function type wrapped in a Πi,
; and the input type for that function, and generates the output type
; of an application on that argument
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(define (all-inner-Πi-args exp)
  (match exp
    [`(Πi ([,v ,τ]) ,e)
     (let-values ([(v-tys vars e) (all-inner-Πi-args e)])
       (values (cons `(,v . ,τ) v-tys) (cons v vars) e))]
    [else (values '() '() exp)]))

(define (restore-Πi new-exp old-exp)
  (match* (new-exp old-exp)
    [(_ (? symbol?))
     new-exp]
    [(`(add1 ,n1) `(add1 ,n2))
     `(add1 ,(restore-Πi n1 n2))]
    [(`(List ,e1) `(List ,e2))
     `(List ,(restore-Πi e1 e2))]
    [(`(Pair ,A1 ,D1) `(Pair ,A2 ,D2))
     `(Pair ,(restore-Πi A1 A2) ,(restore-Πi D1 D2))]
    [(`(Vec ,E1 ,n1) `(Vec ,E2 ,n2))
     `(Vec ,(restore-Πi E1 E2) ,(restore-Πi n1 n2))]
    [(`(-> ,A1 ,B1) `(-> ,A2 ,B2))
     `(Π ([faux-var ,(restore-Πi A1 A2)]) ,(restore-Πi B1 B2))]
    [(`(Σ ([,x ,A1]) ,B1) `(Σ ([,y ,A2]) ,B2))
     `(Σ ([,x ,(restore-Πi A1 A2)]) ,(restore-Πi B1 B2))]
    [(`(Π ([,x ,A1]) ,B1) `(Π ([,y ,A2]) ,B2))
     `(Π ([,x ,(restore-Πi A1 A2)]) ,(restore-Πi B1 B2))]
    [(`(Πi ([,x ,A1]) ,B1) `(Πi ([,y ,A2]) ,B2))
     `(Πi ([,x ,(restore-Πi A1 A2)]) ,(restore-Πi B1 B2))]
    [(`(-> ,A1 ,B1) `(Π ([,x ,A2]) ,B2))
     `(Π ([faux-var ,(restore-Πi A1 A2)]) ,(restore-Πi B1 B2))]
    [(`(Π ([,x ,A1]) ,B1) `(-> ,A2 ,B2))
     `(Π ([,x ,(restore-Πi A1 A2)]) ,(restore-Πi B1 B2))]
    [(_ `(Πi ([,x ,A]) ,B)) `(Πi ([,x ,A]) ,(restore-Πi new-exp B))]))

(define (make-varso vars ans)
  (condu
   [(== vars '()) (== ans '())]
   [(fresh (x xs a anso) (== vars `(,x . ,xs))
           (make-varso xs anso)
           (== ans `((,x ,a) . ,anso)))]))

;; a new version, that also returns the value
(define (solve-arg τ-rator τ-arg)
  (define saved-vars #f)
  (define old #f)
  (define search
    (match τ-rator
      [`(Πi ([,x ,A]) ,B)
       (let-values ([(v-tys vars B) (all-inner-Πi-args τ-rator)])
         (set! saved-vars v-tys)
         (set! old (match B [`(-> ,A ,C) C] [`(Π ([,x ,A]) ,C) C] [else #f]))
         (engine (λ (_) (run 1 (o vs)
                             (fresh (y)
                                    (make-varso vars vs)
                                    (unify-types vs 0 B
                                                 `(Π ([,y ,τ-arg]) ,o) '() '() #t))))))]))
  (let ([inf (engine-run 5000 search)])
    (define (elim-fresh vars)
      (foldr (λ (v ans)
               (if (not (symbol? v)) (cons v ans)
                   (match (string->list (symbol->string v))
                     [`(#\_ #\. . ,whatever) (cons 'sole ans)]
                     [_ (cons v ans)])))
             '()
             vars))
    (match (engine-result search)
      ['() #f]
      [#f #f]
      [`((,τ-res ,τ-arg)) (cons (restore-Πi τ-res old)
                                (elim-fresh (map cadr τ-arg)))])))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Solve: takes two types, one of which has an
; implicit argument, and unifies them, if possible
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(define (solve τ1 τ2)
  (define search
    (match* (τ1 τ2)
      [(`(Πi ([,x1 ,A1]) ,B1) `(Πi ([,x2 ,A2]) ,B2))
       (engine (λ (_) (run 1 (?) (unify-types '() 0 τ1 τ2 '() '()))))]
      [(`(Πi ([,x1 ,A1]) ,B1) _)
       (engine (λ (_) (run 1 (?) (fresh (v) (unify-types `((,x1 ,v)) 0 B1 τ2 '() '() #f)))))]
      [(_ _) (engine (λ (_) #f))]))
  (let ([inf (engine-run 5000 search)])
    (match (engine-result search)
      [#f #f]
      ['() #f]
      [`(,ans) ans])))

(define (unify-types vars lvl e1 e2 xs1 xs2 in-arg-mode?)
  (condu
   ;; Case 1 -- we have a variable we need to unify, and
   ;; the type it needs to be
   [(assvo e1 vars `(,e1 ,e2))]
   ;; Simple base cases
   [(== e1 'U) (== e2 'U)]
   [(== e1 'Atom) (== e2 'Atom)]
   [(fresh (s1) (== e1 `(quote ,s1)) (== e2 `(quote ,s1)))]
   [(== e1 'Nat) (== e2 'Nat)]
   [(== e1 'Trivial) (== e2 'Trivial)]
   [(== e1 'sole) (== e2 'sole)]
   [(== e1 'Absurd) (== e2 'Absurd)]
   [(== e1 'nil) (== e2 'nil)]
   [(== e1 'vecnil) (== e2 'vecnil)]
   ;; Nats
   [(== e1 'zero) (== e2 'zero)]
   [(fresh (n1 n2) (== e1 `(add1 ,n1)) (== e2 `(add1 ,n2))
           (unify-types vars lvl n1 n2 xs1 xs2 in-arg-mode?))]
   [(fresh (nat-elim target1 base1 step1 target2 base2 step2) (== e1 `(,nat-elim ,target1 ,base1 ,step1))
           (membero nat-elim `(which-Nat iter-Nat rec-Nat))
           (== e2 `(which-Nat ,target2 ,base2 ,step2))
           (unify-types vars lvl target1 target2 xs1 xs2 in-arg-mode?)
           (unify-types vars lvl base1 base2 xs1 xs2 in-arg-mode?)
           (unify-types vars lvl base1 base2 xs1 xs2 in-arg-mode?))]
   ;; Variables -- either they are the same De Bruijn index, or both unbound
   ;; and are the same symbol
   [(fresh (v1 v2)
           (symbolo e1)
           (symbolo e2)           
           (varo e1)
           (varo e2)
           
           (conde
            [(== v1 #f) (== v2 #f) (== e1 e2) 
                        (assvo e1 xs1 v1)
                        (assvo e2 xs2 v2)]
            [(fresh (index)
                    (== v1 `(,e1 . ,index))
                    (== v2 `(,e2 . ,index))
                    (assvo e1 xs1 v1)
                    (assvo e2 xs2 v2))]))]
   ;; Pi and Pii Types
   [(fresh (A1 B1)
           (== e1 `(-> ,A1 ,B1))
           (condu
            [(fresh (A2 B2) (== e2 `(-> ,A2 ,B2))
                    (unify-types vars lvl A1 A2 xs1 xs2 in-arg-mode?)
                    (unify-types vars lvl B1 B2 xs1 xs2 in-arg-mode?))]
            [(fresh (x A2 B2) (== e2 `(Π ([,x ,A2]) ,B2))
                    (unify-types vars lvl A1 A2 xs1 xs2 in-arg-mode?)
                    (unify-types vars lvl B1 B2 xs1 xs2 in-arg-mode?))]))]
   [(fresh (x1 A1 D1 xs1^ xs2^)
           (== e1 `(Π ([,x1 ,A1]) ,D1))
           (condu
            [(fresh (x2 A2 D2) (== e2 `(Π ([,x2 ,A2]) ,D2))
                    (unify-types vars lvl A1 A2 xs1 xs2 in-arg-mode?)
                    (condu
                     [(== in-arg-mode? #f) (== xs1^ `((,x1 . ,lvl) . ,xs1))]
                     [(== in-arg-mode? #t) (== xs1^ `((faux-var . ,lvl) . ,xs1))])
                    (== xs2^ `((,x2 . ,lvl) . ,xs2))
                    (unify-types vars (add1 lvl) D1 D2 xs1^ xs2^ in-arg-mode?))]
            [(fresh (x2 A2 D2)
                    (== e2 `(-> ,A2 ,D2))
                    (unify-types vars lvl A1 A2 xs1 xs2 in-arg-mode?)
                    (unify-types vars lvl D1 D2 xs1 xs2 in-arg-mode?))]))]
   ; when we encounter a Πi:
   ;-- the other expression could also be a Πi, in which case it's easy
   ;-- the other expression could be something else,
   ;   in which case we add unknown variables to our list
   [(fresh (x1 A1 D1)
           (== e1 `(Πi ([,x1 ,A1]) ,D1))
           (condu [(fresh (x2 A2 D2 xs1^ xs2^) (== e2 `(Πi ([,x2 ,A2]) ,D2))
                          (condu [(== in-arg-mode? #t) (== x1 x2)]
                                 [(== in-arg-mode? #f)])
                          (unify-types vars lvl A1 A2 xs1 xs2 in-arg-mode?)
                          (== xs1^ `((,x1 . ,lvl) . ,xs1))
                          (== xs2^ `((,x2 . ,lvl) . ,xs2))
                          (unify-types vars (add1 lvl) D1 D2 xs1^ xs2^ in-arg-mode?))]
                  [(unify-types vars lvl D1 e2 xs1 xs2 in-arg-mode?)
                   #;(fresh (xs1^)
                          (== xs1^ `((,x1 . ,lvl) . ,xs1))
                          (unify-types vars (add1 lvl) D1 e2 xs1^ xs2 in-arg-mode?))]))]
   [(fresh (x1 b1 x2 b2 xs1^ xs2^)
           (== e1 `(λ (,x1) ,b1))
           (== e2 `(λ (,x2) ,b2))
           (== xs1^ `((,x1 . ,lvl) . ,xs1))
           (== xs2^ `((,x2 . ,lvl) . ,xs2))
           (unify-types vars (add1 lvl) b1 b2 xs1^ xs2^ in-arg-mode?))]
   ;; Sigma Types
   [(fresh (A1 D1 A2 D2)
           (== e1 `(Pair ,A1 ,D1))
           (== e2 `(Pair ,A2 ,D2))
           (unify-types vars lvl A1 A2 xs1 xs2 in-arg-mode?)
           (unify-types vars lvl D1 D2 xs1 xs2 in-arg-mode?))]
   [(fresh (x1 A1 D1 x2 A2 D2 xs1^ xs2^)
           (== e1 `(Σ ([,x1 ,A1]) ,D1))
           (== e2 `(Σ ([,x2 ,A2]) ,D2))
           (unify-types vars lvl A1 A2 xs1 xs2 in-arg-mode?)
           (== xs1^ `((,x1 . ,lvl) . ,xs1))
           (== xs2^ `((,x2 . ,lvl) . ,xs2))
           (unify-types vars (add1 lvl) D1 D2 xs1^ xs2^ in-arg-mode?))]
   [(fresh (a1 d1 a2 d2)
           (== e1 `(cons ,a1 ,d1))
           (== e2 `(cons ,a2 ,d2))
           (unify-types vars lvl a1 a2 xs1 xs2 in-arg-mode?)
           (unify-types vars lvl d1 d2 xs1 xs2 in-arg-mode?))]
   [(fresh (pr1 pr2)
           (== e1 `(car ,pr1))
           (== e2 `(car ,pr2))
           (unify-types vars lvl pr1 pr2 xs1 xs2 in-arg-mode?))]
   [(fresh (pr1 pr2)
           (== e1 `(cdr ,pr1))
           (== e2 `(cdr ,pr2))
           (unify-types vars lvl pr1 pr2 xs1 xs2 in-arg-mode?))]
   [(fresh (a1 d1)
           (== e1 `(car (cons ,a1 ,d1)))
           (unify-types vars lvl a1 e2 xs1 xs2 in-arg-mode?))]
   [(fresh (a1 d1)
           (== e1 `(cdr (cons ,a1 ,d1)))
           (unify-types vars lvl d1 e2 xs1 xs2 in-arg-mode?))]
   [(fresh (pr)
           (== e1 `(cons (car ,pr) (cdr ,pr)))
           (unify-types vars lvl pr e2 xs1 xs2 in-arg-mode?))]
   ;; Vectors -- inc
   [(fresh (E1 n1 E2 n2) (== e1 `(Vec ,E1 ,n1))
           (== e2 `(Vec ,E2 ,n2))
           (unify-types vars lvl E1 E2 xs1 xs2 in-arg-mode?)
           (unify-types vars lvl n1 n2 xs1 xs2 in-arg-mode?))]
   [(fresh (h1 t1 h2 t2) (== e1 `(vec:: ,h1 ,t1))
           (== e2 `(vec:: ,h2 ,t2))
           (unify-types vars lvl h1 h2 xs1 xs2 in-arg-mode?)
           (unify-types vars lvl t1 t2 xs1 xs2 in-arg-mode?))]
   ;; Lists -- inc
   [(fresh (E1 E2) (== e1 `(List ,E1))
           (== e2 `(List ,E2))
           (unify-types vars lvl E1 E2 xs1 xs2 in-arg-mode?))]
   [(fresh (a1 d1 a2 d2) (== e1 `(:: ,a1 ,d1))
           (== e2 `(:: ,a2 ,d2))
           (unify-types vars lvl a1 a2 xs1 xs2 in-arg-mode?)
           (unify-types vars lvl d1 d2 xs1 xs2 in-arg-mode?))]
   ;; Application
   [(fresh (rator1 rand1 rator2 rand2)
           (== e1 `(,rator1 ,rand1))
           (== e2 `(,rator2 ,rand2))
           (unify-types vars lvl rator1 rator2 xs1 xs2 in-arg-mode?)
           (unify-types vars lvl rand1 rand2 xs1 xs2 in-arg-mode?))]
   ))


;; silly function to curry input type -- NOT being used, but i don't feel like deleting it
(define (curry exp)
  (match exp
    [(? symbol?) exp]
    [`(add1 ,n) `(add1 ,(curry n))]
    [`(List ,e) `(List ,(curry e))]
    [`(Vec ,e ,n) `(Vec ,(curry e) ,(curry n))]
    [`(-> ,A ,B) `(-> ,(curry A) ,(curry B))]
    [`(-> ,As ... ,B) (curry-> As (curry B))]
    [`(Π ([,x ,A]) ,B) `(Π ([,x ,(curry A)]) ,(curry B))]
    [`(Π ,args ,B) (curry-Π args (curry B))]
    [`(Πi ([,x ,A]) ,B) `(Πi ([,x ,(curry A)]) ,(curry B))]
    [`(Πi ,args ,B) (curry-Πi args (curry B))]))

(define (curry-> As B)
  (foldr (λ (A b) `(-> ,(curry A) ,b)) B As))
(define (curry-Π args B)
  (foldr (λ (A b)
           (match A [`(,x ,a) `(Π ([,x ,(curry A)]) ,b)]))
         B args))
(define (curry-Πi args B)
  (foldr (λ (A b)
           (match A [`(,x ,a) `(Πi ([,x ,(curry A)]) ,b)]))
         B args))


(define (apply-renaming r exp)
  (match exp
    [(? symbol?) (match (assv exp r)
                   [#f exp]
                   [`(,x . ,y) y])]
    [`(add1 ,n) `(add1 ,(apply-renaming r n))]
    [`(List ,e) `(List ,(apply-renaming r e))]
    [`(Vec ,e ,n) `(Vec ,(apply-renaming r e) ,(apply-renaming r n))]
    [`(-> ,A ,B) `(-> ,(apply-renaming r A) ,(apply-renaming r B))]
    [`(Π ([,x ,A]) ,B) `(Π ([,x ,(apply-renaming r A)]) ,(apply-renaming r B))]
    [`(Πi ([,x ,A]) ,B) `(Πi ([,(apply-renaming r x) ,(apply-renaming r A)]) ,(apply-renaming r B))]))